# Class Name: ItemCommands.sk
# Author: nac#3030
# Deps: skript-YAML, SkBee

# Gives the player a sword with the specified rarity.
command /giveWeapon <text> <text>:
	permission: admin.weapon.give
	trigger:
		Give_Weapon(arg-1, arg-2, player)
		send "Given weapon &e'%arg-1%&e'&f with rarity &e'%arg-2%&e'&f." to player

# Register a sword in-game.
command /registerWeapon <text> <text> <num>:
	permission: admin.weapon.register
	trigger:
		set {_a2} to arg-2
		replace all """" with "" in {_a2}

		load yaml "plugins/Skript/scripts/XS-Data/properties/weapons.yaml" as "weapons.yaml"

		if yaml node arg-1 in "swords.yaml" exists:
			send "This entry already exists." to player
		else:
			set {_enchantments::*} to enchantments of player's tool
			Weapon_Register(arg-1, player's tool, {_a2}, {_enchantments::*}, arg-3)
			send "Registerd weapon &e'%arg-1%&e'&f with display &e'%colored arg-2%&e'&f." to player
	
		save yaml "weapons.yaml"
		unload yaml "weapons.yaml"

# Register a rarity in-game.
command /registerRarity <text> <text> <num> <text>:
	permission: admin.rarity.register
	trigger:
		set {_a2} to arg-2
		replace all """" with "" in {_a2}
		
		load yaml "plugins/Skript/scripts/XS-Data/properties/rarities.yaml" as "rarities.yaml"

		if yaml node arg-1 in "rarities.yaml" exists:
			send "This entry already exists." to player
		else:
			Rarity_Register(arg-1, arg-2, arg-3, arg-4)
			send "Registerd rarity &e'%arg-1%&e'&f with display &e'%colored arg-2%&e'&f." to player
		
		save yaml "rarities.yaml"
		unload yaml "rarities.yaml"

# Tab completions for /giveSword.
on tab complete of "/giveWeapon":
	set tab completions for position 1 to GetRootNodes("plugins/Skript/scripts/XS-Data/properties/weapons.yaml")
	set tab completions for position 2 to GetRootNodes("plugins/Skript/scripts/XS-Data/properties/rarities.yaml")

# Tab completions for /registerRarity.
on tab complete of "/registerRarity":
	set tab completions for position 4 to "MULTIPLY" and "ADD"

on damage:

	load yaml "plugins/Skript/scripts/XS-Data/properties/rarities.yaml" as "rarities.yaml"
	load yaml "plugins/Skript/scripts/XS-Data/properties/weapons.yaml" as "weapons.yaml"

	set {_weaponId} to "YAML-ID" tag of nbt of attacker's tool
	set {_weaponRarity} to "YAML-Rarity" tag of nbt of attacker's tool

	set {_weaponDamage} to yaml value "%{_weaponId}%.Damage" in "weapons.yaml"
	set {_weaponDamage.rarityMod} to yaml value "%{_weaponRarity}%.Damage.Multi" in "rarities.yaml"
	set {_weaponDamage.rarityMod.Type} to yaml value "%{_weaponRarity}%.Damage.Type" in "rarities.yaml"

	if {_weaponDamage.rarityMod.Type} is "ADD":
		set {_dmg} to {_weaponDamage} + {_weaponDamage.rarityMod}
	else if {_weaponDamage.rarityMod.Type} is "MULTIPLY":
		set {_dmg} to {_weaponDamage} * {_weaponDamage.rarityMod}

	unload yaml "rarities.yaml"
	unload yaml "weapons.yaml"

	set damage to {_dmg}

	send action bar "&4%{_dmg} ? damage%" to attacker